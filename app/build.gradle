plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

apply from: "../versions.gradle"

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

def apiKey = localProperties.getProperty('API_KEY', '')

android {
    namespace 'is.quot'
    compileSdk versions.compileSdk

    defaultConfig {
        applicationId "is.quot"
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode versions.versionCode
        versionName "$versions.versionName"

        buildConfigField("String", "API_KEY", "${apiKey}")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.4'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}

dependencies {

    // AndroidX
    implementation "androidx.core:core-ktx:$versions.coreKtx"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.runtimeKtx"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.runtimeKtx"
    implementation "androidx.activity:activity-compose:$versions.composeActivity"
    implementation platform("androidx.compose:compose-bom:$versions.compose")
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation "com.google.accompanist:accompanist-coil:$versions.accompanistCoil"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$versions.accompanistSystemUiController"

    // Kotlin
    implementation platform("org.jetbrains.kotlin:kotlin-bom:$versions.kotlinBom")

    // Hilt
    implementation "com.google.dagger:hilt-android:$versions.hilt"
    kapt "com.google.dagger:hilt-compiler:$versions.hilt"

    // Coil & Glide
    implementation "io.coil-kt:coil-compose:$versions.coil"
    implementation "io.coil-kt:coil-gif:$versions.coil"
    implementation "com.github.skydoves:landscapist-glide:$versions.glide"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okHttp"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$versions.okHttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okHttp"

    // Moshi
    implementation "com.squareup.moshi:moshi:$versions.moshi"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"
    implementation "com.squareup.moshi:moshi-kotlin:$versions.moshi"
    implementation "com.squareup.moshi:moshi-adapters:$versions.moshi"

    // Lottie
    implementation "com.airbnb.android:lottie:$versions.lottie"
    implementation "com.airbnb.android:lottie-compose:$versions.lottie"

    // Logging
    api "com.jakewharton.timber:timber:$versions.timber"

    // Third-party
    implementation "com.github.commandiron:ComposeLoading:$versions.composeLoading"

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}